plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id "com.github.node-gradle.node" version "7.0.2"
}

group = 'com.drodzewicz'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

node {
	// specify the Node.js version
	version = '20.12.2'  
	// specify the npm version (optional)
    npmVersion = '10.5.0'
	// set to true to download the specified Node.js version
	download = false
	// identify working directory in project
	workDir = file("${project.projectDir}/src/main/frontend-ui/javascript/nodejs")
	// NPM work directory
	// npmWorkDir = file("${project.projectDir}/src/main/frontend-ui/javascript/npm")
}

task appNpmInstall(type: NpmTask){
	description = "Read packge.json and install all jars"
	workingDir = file("${project.projectDir}/src/main/frontend-ui/javascript")
	args =["install"]
}

task appNpmBuild(type: NpmTask){
	description = "Builds applciation for your frontend "
	args =["run", "build"]
	workingDir = file("${project.projectDir}/src/main/frontend-ui/javascript")
}

task copyUIBuild(type: Copy){
	from file("${project.projectDir}/src/main/frontend-ui/javascript/dist/")
	into file("${project.projectDir}/src/main/resources/static")
}

// sequence of dependancies
appNpmBuild.dependsOn appNpmInstall
copyUIBuild.dependsOn appNpmBuild
processResources.dependsOn copyUIBuild
compileJava.dependsOn copyUIBuild

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// spring
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation:3.2.1'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	// utils
	implementation 'com.google.guava:guava:33.2.1-jre'
	// security
	implementation "org.springframework.boot:spring-boot-starter-security"
	// testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// Mapstruct
   	compileOnly 'org.mapstruct:mapstruct:1.4.2.Final'
   	annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
   	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// postgress
	runtimeOnly 'org.postgresql:postgresql'
	// h2
	runtimeOnly 'com.h2database:h2'
	// session
	implementation 'org.springframework.session:spring-session-jdbc'
	// liquibase
	implementation 'org.liquibase:liquibase-core:4.28.0'
	//swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
}

processResources {
   filesMatching('**/application.properties') {
		expand(project.properties)
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
